local cfg_set=(.zshenv .zprofile .zshrc .zlogin .zlogout)
local cfg_add=(.p10k{,-ascii}{,-8color}.zsh)

local args=("$@")
local pos=()

while (( $# )); do
  case "$1" in
    --) pos+=("${@:2}"); break;;
    -*) [[ bcDEeFIiJLlmOopQRSWw == *${1[-1]}* ]] && shift;;
    *)  pos+=("$1");;
  esac
  (( $# )) && shift
done

local srv
if [[ $#pos != 1 || -z ${srv::=${pos[1]##*@}} ]] || zstyle -t :z4h:ssh:$srv passthrough; then
  command ssh "${args[@]}"
  return
fi

local version
IFS= read -r version <$Z4H/zsh4humans/version || return

local dump
dump="$(
  builtin cd -q -- $ZDOTDIR || exit
  local files=($^cfg_set(N^/) $^cfg_add(N^/))
  if (( ! $files[(I).zshrc] )); then
    print -Pru2 -- '%F{3}z4h%f: file not found: %F{1}'${ZDOTDIR//\%/%%}'/.zshrc%f'
    exit 1
  fi
  tar -czh -- $files | base64 | tr -d '\n')" || return

command ssh -t "${args[@]}" '"set" "-ue" || "exit"
"export" ZDOTDIR="$HOME"
"export" Z4H="${XDG_CACHE_HOME:-$HOME/.cache}"/zsh4humans
"export" Z4H_SSH='${(qqq)srv}'
"export" LC_ALL="C"
(
  IFS="" "read" "-r" "_z4h_v" <"$Z4H"/zsh4humans/version &&
    "[" "$_z4h_v" "=" '${(qqq)version}' "]"
) 2>/dev/null || "command" "rm" "-rf" "--" "$Z4H" || "exit"
"command" "mkdir" "-p" "--" "$ZDOTDIR"
(
  if "[" "-n" "${ZSH_VERSION-}" "]"; then
    "emulate" "sh" "-o" "err_return" "-o" "no_unset" "-o" "monitor"
    "builtin" "cd" "-q" "--" "$ZDOTDIR"
  else
    "cd" "--" "$ZDOTDIR"
  fi
  _z4h_d="-D"
  _z4h_w=""
  "echo" "Cg==" | "command" "base64" "-d" >"/dev/null" 2>&1 && _z4h_d="-d"
  "command" "rm" "-rf" "--" '${(j: :)cfg_set}'
  "command" "tar" "--help" 2>&1 | "command" "grep" "-qF" "--" "--warning=" 2>/dev/null && _z4h_w="--warning=no-unknown-keyword"
  "echo" "'$dump'" | "command" "base64" $_z4h_d | "command" "tar" $_z4h_w "-xz"
)
"set" "+ue"
. "$ZDOTDIR"/.zshrc'
