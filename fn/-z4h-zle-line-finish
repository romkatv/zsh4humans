#!/usr/bin/env zsh

local -i ret

if [[ -v widgets[-z4h-orig-zle-line-finish] ]]; then
  zle -- -z4h-orig-zle-line-finish "$@" || ret=$?
fi

if [[ -v _z4h_redraw_fd ]]; then
  zle -F "$_z4h_redraw_fd"
  exec {_z4h_redraw_fd}>&-
  unset _z4h_redraw_fd
fi

if ! zstyle -t :z4h:history preserve-trailing-whitespace; then
  if [[ $BUFFER == *' ' ]]; then
    if [[ -v functions[-z4h-buffer-func] ]]; then
      builtin unfunction -- -z4h-buffer-func
    fi
    if functions[-z4h-buffer-func]="$PREBUFFER$BUFFER" 2>/dev/null &&
       [[ -v functions[-z4h-buffer-func] ]]; then
      builtin unfunction -- -z4h-buffer-func
      BUFFER=${BUFFER%% #}
    fi
  fi
fi

unset POSTDISPLAY _z4h_autosuggest_buffer _z4h_autosuggestion
if [[ -v region_highlight ]]; then
  _zsh_highlight
fi

return ret
